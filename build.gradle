plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

group = 'com.coachticketbooking'
version = '0.0.1-SNAPSHOT'
description = 'Root project for Coach Ticket Booking microservices'

ext {
    springBootVersion = '3.5.6'
    springCloudVersion = '2025.0.0'
}

/* Cho tất cả project dùng chung repo */
allprojects {
    repositories {
        mavenCentral()
    }
}

/* Cấu hình chung cho mọi module con */
subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    group = rootProject.group
    version = rootProject.version

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
        dependencies {
            dependency 'org.apache.commons:commons-lang3:3.18.0'
        }
    }

    tasks.withType(Test).tap {
        configureEach {
            useJUnitPlatform()
        }
    }
}
